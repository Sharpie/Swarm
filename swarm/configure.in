AC_INIT(Makefile.appl.in)

VERSION=`head -1 $srcdir/VERSION`
AM_INIT_AUTOMAKE(Swarm, $VERSION)

AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_PROG_RANLIB

AC_PATH_XTRA
if test "$ac_R_space" = yes ; then
  RPATH='-R '
else 
  if test "$ac_R_nospace" = yes ; then
    RPATH='-R'
  else
    RPATH='-Wl,-rpath '
  fi
fi

AC_ARG_WITH(defaultdir,
[  --with-defaultdir=DIR            Use DIR instead of prefix as default path],
  defaultdir=$withval)

if test -z "$defaultdir" ; then
  if test $prefix = NONE ; then
    defaultdir=/usr/local
  else
    defaultdir=$prefix
  fi
fi

AC_ARG_WITH(tcldir,
[  --with-tcldir=DIR                Use DIR/include and DIR/lib for Tcl],
  tclincludedir=$withval/include
  tcllibdir=$withval/lib)

AC_ARG_WITH(tclincludedir,
[  --with-tclincludedir=DIR         Use DIR for Tcl header files],
  tclincludedir=$withval)

if test -z "$tclincludedir" ; then
  md_FIND_TCL_HEADERS
  if test -z "$tclincludedir" ; then
    AC_MSG_ERROR(Please specify Tcl header path using --with-tcldir or --with-tclincludedir)
  fi
fi

md_FIND_TCL_LIBRARIES
if test -z "$tcllibdir" ; then
  AC_MSG_ERROR(Please specify Tcl library path using --with-tcldir)
fi

AC_ARG_WITH(tkdir,
[  --with-tkdir=TKDIR               Use DIR/include and DIR/lib for Tk],
  tkincludedir=$withval/include
  tklibdir=$withval)

AC_ARG_WITH(tkincludedir,
[  --with-tkincludedir=DIR          Use DIR for Tk header files],
  tkincludedir=$withval/include)

md_FIND_TK_HEADERS
if test -z "$tkincludedir" ; then
  AC_MSG_ERROR(Please specify Tk include path using --with-tkincludedir)
fi

if test -z "$tklibdir" ; then
  md_FIND_TK_LIBRARIES
  if test -z "$tklibdir" ; then
    AC_MSG_ERROR(Please specify Tk library path using --with-tkdir)
  fi
fi

AC_ARG_WITH(tclobjcdir,
[  --with-tclobjcdir=DIR            Use DIR/include and DIR/lib for tclobjc],
  tclobjcdir=$withval)

AC_ARG_WITH(tclobjclibname,
[  --with-tclobjclibname=NAME       Use -lNAME for tclobjc],
  tclobjclibname=$withval)

AC_ARG_WITH(tclobjcincludedir,
[  --with-tclobjcincludedir=DIR     Use DIR for tclobjc headers],
  tclobjcincludedir=$withval)

md_FIND_TCLOBJC_HEADERS
md_FIND_TCLOBJC_LIBRARIES

AC_ARG_WITH(bltdir,
[  --with-bltdir=DIR                Use DIR/include and DIR/lib for BLT],
  bltdir=$withval)

AC_ARG_WITH(bltlibname,
[  --with-bltlibname=NAME           Use -lNAME for BLT],
  bltlibname=$withval)

md_FIND_BLT

AC_ARG_WITH(ffidir,
[  --with-ffidir=DIR                Use DIR/include and DIR/lib for libffi],
  ffidir=$withval)

AC_ARG_WITH(ffcalldir,
[  --with-ffcalldir=DIR             Use DIR/include and DIR/lib
                                     for ffcall (alternative to libffi)])

md_FIND_FFI

AC_ARG_WITH(zlibdir,
[  --with-zlibdir=DIR               Use DIR/include and DIR/lib for zlib],
  zlibdir=$withval)

md_FIND_ZLIB

AC_ARG_WITH(pngdir,
[  --with-pngdir=DIR                Use DIR/include and DIR/lib for png],
  pngdir=$withval)

md_FIND_PNG

AC_CHECK_LIB(dl,main)

AC_OUTPUT([Makefile src/Makefile src/activity/Makefile src/analysis/Makefile src/collections/Makefile src/defobj/Makefile src/gui/Makefile src/misc/Makefile src/objectbase/Makefile src/random/Makefile src/simtools/Makefile src/simtoolsgui/Makefile src/space/Makefile src/tkobjc/Makefile Makefile.appl Makefile.lib])
