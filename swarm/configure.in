AC_INIT(etc/Makefile.appl.in)

VERSION=`head -1 $srcdir/VERSION`
AM_INIT_AUTOMAKE(swarm, $VERSION)
md_SWARM_INSTALLER

if test "x$ACLOCAL" = xaclocal; then
  ACLOCAL="aclocal -I m4"
fi

AM_CONFIG_HEADER(swarmconfig.h)

AC_PROG_CC
AM_PROG_LIBTOOL

AC_PATH_XTRA
md_CHECK_GUILIBS
md_CHECK_EXESUFFIX
md_CHECK_LINKFLAGS

AC_CHECK_LIB(dl,main,DLLIB=-ldl)
AC_CHECK_LIB(dld,main,DLLIB=-ldld) # HPUX
AC_SUBST(DLLIB)

md_CHECK_RPATH
if test $enable_shared = yes ; then
  SWARMLDFLAGS="-L\$(libdir) $RPATH\$(libdir)"
else
  SWARMLDFLAGS="-L\$(libdir)"
fi
AC_SUBST(SWARMLDFLAGS)

AC_ARG_WITH(defaultdir,
[  --with-defaultdir=DIR            Use DIR instead of prefix as default path],
  defaultdir=$withval)

if test -z "$defaultdir" ; then
  if test $prefix = NONE ; then
    defaultdir=/usr/local
  else
    defaultdir=$prefix
  fi
fi

AC_ARG_WITH(tcldir,
[  --with-tcldir=DIR                Use DIR/include and DIR/lib for Tcl],
  tclincludedir=$withval/include
  tcllibdir=$withval/lib)

AC_ARG_WITH(tclincludedir,
[  --with-tclincludedir=DIR         Use DIR for Tcl header files],
  tclincludedir=$withval)

AC_ARG_WITH(tcllibname,
[  --with-tcllibname=NAME           Use -lNAME for Tcl],
  tcllibname=$withval)

md_FIND_TCL_HEADERS
if test -z "$tclincludedir" ; then
  AC_MSG_ERROR(Please specify Tcl header path using --with-tcldir or --with-tclincludedir)
fi

md_FIND_TCL_LIBRARIES
if test -z "$tcllibdir" ; then
  AC_MSG_ERROR(Please specify Tcl library path using --with-tcldir)
fi

AC_ARG_WITH(tkdir,
[  --with-tkdir=TKDIR               Use DIR/include and DIR/lib for Tk],
  tkincludedir=$withval/include
  tklibdir=$withval/lib)

AC_ARG_WITH(tkincludedir,
[  --with-tkincludedir=DIR          Use DIR for Tk header files],
  tkincludedir=$withval)

AC_ARG_WITH(tklibname,
[  --with-tklibname=NAME            Use -lNAME for Tk],
  tklibname=$withval)

md_FIND_TK_HEADERS
if test -z "$tkincludedir" ; then
  AC_MSG_ERROR(Please specify Tk include path using --with-tkincludedir)
fi

md_FIND_TK_LIBRARIES
if test -z "$tklibdir" ; then
  AC_MSG_ERROR(Please specify Tk library path using --with-tkdir)
fi

AC_ARG_WITH(bltdir,
[  --with-bltdir=DIR                Use DIR/include and DIR/lib for BLT],
  bltdir=$withval)

AC_ARG_WITH(bltlibname,
[  --with-bltlibname=NAME           Use -lNAME for BLT],
  bltlibname=$withval)

md_FIND_BLT

AC_ARG_WITH(ffidir,
[  --with-ffidir=DIR                Use DIR/include and DIR/lib for libffi],
  ffidir=$withval)

AC_ARG_WITH(ffcalldir,
[  --with-ffcalldir=DIR             Use DIR/include and DIR/lib
                                     for ffcall (alternative to libffi)])

md_FIND_FFI

AC_ARG_WITH(zlibdir,
[  --with-zlibdir=DIR               Use DIR/include and DIR/lib for zlib],
  zlibdir=$withval
  zlibincludedir=$withval/include)

AC_ARG_WITH(zlibincludedir,
[  --with-zlibincludedir=DIR        Use DIR for zlib include files],
  zlibincludedir=$withval)

md_FIND_ZLIB

AC_ARG_WITH(pngdir,
[  --with-pngdir=DIR                Use DIR/include and DIR/lib for PNG],
  pngdir=$withval
  pngincludedir=$withval/include)

AC_ARG_WITH(pngincludedir,
[  --with-pngincludedir=DIR         Use DIR for PNG include files],
  pngincludedir=$withval)

md_FIND_PNG
md_CHECK_INLINING
md_CHECK_OBJC_LIBS
md_CHECK_NEXTSTEP
md_CHECK_POINTER_FMT
md_CHECK_BUILTIN_APPLY


ac_save_CFLAGS=$CFLAGS
CFLAGS="$CFLAGS -fno-builtin"
md_REPLACE_FUNCS(memcpy memset strcmp strlen)
CFLAGS=$ac_save_CFLAGS

md_REPLACE_FUNCS(memchr realpath stpcpy strchr strdup  strncmp strndup strnlen strpbrk strsep)

if test $ac_cv_func_stpcpy = yes || test $ac_cv_func_strndup = yes || test $ac_cv_func_strnlen = yes; then
  CPPFLAGS="$CPPFLAGS -D_GNU_SOURCE"
fi

if test $prefix = /usr; then
  includedir=${includedir}/swarm
  libdir=${libdir}/swarm
fi

swarm_prefix=$prefix
if test $exec_prefix = NONE; then
  swarm_exec_prefix='${swarm_prefix}'
else
  swarm_exec_prefix=$exec_prefix
fi
swarm_libdir="`echo $libdir | sed 's/{/{swarm_/'`"
AC_SUBST(swarm_prefix)
AC_SUBST(swarm_exec_prefix)
AC_SUBST(swarm_libdir)

AC_DEFINE_UNQUOTED(DATADIR,"`eval echo $datadir/swarm/`")
AC_DEFINE_UNQUOTED(SYSCONFDIR,"`eval echo $sysconfdir/swarm/`")


AC_OUTPUT([Makefile m4/Makefile src/Makefile src/activity/Makefile src/analysis/Makefile src/collections/Makefile src/defobj/Makefile src/gui/Makefile src/misc/Makefile src/objectbase/Makefile src/random/Makefile src/simtools/Makefile src/simtoolsgui/Makefile src/space/Makefile src/tclobjc/Makefile src/tkobjc/Makefile src/tkobjc/tk/Makefile etc/config.swarm etc/Makefile.appl etc/Makefile.lib etc/Makefile
])


