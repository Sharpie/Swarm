AC_INIT(userbook.sgml)

AM_INIT_AUTOMAKE(swarm-userguide, 0.0)

if test "x$ACLOCAL" = xaclocal; then
  ACLOCAL="aclocal -I m4"
fi

AC_ARG_WITH(catalog,dnl
  --with-catalog=FILE     Use catalog at FILE,dnl
  catalog_file=$withval,dnl
  catalog_file="")

AC_ARG_WITH(dtd-common,dnl
  --with-dtd-common=DIR   Common Declarations DTD in DIR,dnl
  commondtd_srcdir=$withval,dnl
  commondtd_srcdir="")

AC_ARG_WITH(dtd,dnl
changequote(<,>)dnl
  --with-dtd=DIR          DocBook DTD in DIR [${DOCBOOKDTDDIR:-/DocBookDTD}],dnl
changequote([,])dnl
  docbookdtd_srcdir=$withval,dnl
  docbookdtd_srcdir=${DOCBOOKDTDDIR:-/DocBookDTD})

AC_ARG_WITH(stylesheet,dnl
changequote(<,>)dnl
  --with-stylesheet=DIR   DocBook DSSSL in DIR [${DOCBOOKSTYLEDIR:-/DocBookStyleSheets}],dnl
changequote([,])dnl
  docbookstyle_srcdir=$withval,dnl
  docbookstyle_srcdir=${DOCBOOKSTYLEDIR:-/DocBookStyleSheets})


AM_CONFIG_HEADER(config.h)

AC_SUBST(swarm_srcdir)

dnl Change to the directory, since the text may be symbolic or a relative path
changequote(<,>)dnl
top_dossrcdir=`cd $srcdir; pwd | sed 's,//\([A-Za-z]\),\1:,'`
dnl Eval command, since default is symbolic SWARMSRCDIR
swarm_dossrcdir=`eval cd $swarm_srcdir ; pwd | sed 's,//\([A-Za-z]\),\1:,'`
changequote([,])dnl
AC_SUBST(top_dossrcdir)
AC_SUBST(swarm_dossrcdir)

if test -z "$catalog_file" ; then
  test -n "$commondtd_srcdir" && catopts="-c $commondtd_srcdir/catalog"
  catopts="$catopts -c $docbookdtd_srcdir/docbook.cat -c $docbookstyle_srcdir/catalog"
else
  catopts="-c $catalog_file"
fi
AC_SUBST(catopts)

if test -f "$docbookstyle_srcdir/bin/collateindex.pl" ; then
  COLLATEINDEX="$docbookstyle_srcdir/bin/collateindex.pl"
  AC_SUBST(COLLATEINDEX)
else
  AC_PATH_PROG(COLLATEINDEX, collateindex.pl, missing)
fi

AC_PATH_PROG(EMACS, emacs, missing)
AC_PATH_PROG(JADE, jade, missing)
AC_PATH_PROG(PNGTOPNM, pngtopnm, missing)
AC_PATH_PROG(PNMCROP, pnmcrop, missing)
AC_PATH_PROG(PNMTOPS, pnmtops, missing)
AC_PATH_PROG(CJPEG, cjpeg, missing)

dnl Don't use path here since TeX can't deal with Cygwin UNC paths.
AC_CHECK_PROG(JADETEX, jadetex, jadetex, missing)
AC_CHECK_PROG(DVIPS, dvips, dvips, missing)

for i in EMACS JADE PNGTOPNM PNMCROP PNMTOPS CJPEG; do
if eval test '$ac_cv_path_'$i = missing ; then
AC_MSG_ERROR(Failed to find $i)
fi
done

for i in JADETEX DVIPS; do
if eval test '$ac_cv_prog_'$i = missing ; then
AC_MSG_ERROR(Failed to find $i)
fi
done

AC_SUBST(SGML_CATALOG_FILES)
md_PROG_GS

AC_OUTPUT([Makefile catalog userbook.tex figs/Makefile])
