AC_INIT(history.el)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(swarmdocs, 2003-09-08)

AM_MAINTAINER_MODE()

AC_PROG_LN_S()

if test "x$ACLOCAL" = xaclocal; then
  ACLOCAL="aclocal -I m4"
fi

AC_ARG_WITH(swarm,
[AC_HELP_STRING(--with-swarm=DIR,[Swarm in DIR [$SWARMSRCDIR]])],
  swarm_srcdir=$withval,
  swarm_srcdir='${SWARMSRCDIR}')

AC_ARG_WITH(catalog,
[AC_HELP_STRING(--with-catalog=FILE,[Use XML catalog in FILE [/etc/xml/catalog]])],
  catalog_file=$withval,
  catalog_file='/etc/xml/catalog')

AC_ARG_WITH(javadoc,
[AC_HELP_STRING(--with-javadoc=DIR,[Enable generation of Java documentation using JDK installed in DIR])],
  javadoc=$withval)
AC_MSG_CHECKING(do we build Java documentation?)

test -z "$javadoc" && javadoc=no
if test $javadoc = no; then
  AC_MSG_RESULT(no)
  JAVADOC=
  JAVADOCDIR=
else
  AC_MSG_RESULT(yes)
  MACOSJAVA=no
  expand_javadoc=`eval echo $javadoc`
  USEDOSCLASSPATH=no
  if test $expand_javadoc = /System/Library/Frameworks/JavaVM.framework; then
    MACOSJAVA=yes
	AC_PATH_PROG(JAVADOC, javadoc, missing, $javadoc/Commands)
  else
	AC_PATH_PROG(JAVADOC, javadoc, missing, $javadoc/bin)
  fi
  if test $JAVADOC != missing; then
    AC_MSG_RESULT(yes)
    JAVADOCDIR="javadoc"
  else
    AC_MSG_ERROR([Please specify a correct location for the JDK])
  fi
  AC_CHECK_PROG(LATEX, latex, latex, missing)
  if test $LATEX = missing; then
    AC_MSG_ERROR([Failed to find latex])
  fi
fi
AC_SUBST(JAVADOC)
AC_SUBST(JAVADOCDIR)

AM_CONFIG_HEADER(config.h)

AC_SUBST(swarm_srcdir)

dnl Change to the directory, since the text may be symbolic or a relative path
if test "$host_os" = cygwin; then
  top_dossrcdir=`cygpath -w $srcdir`
  dnl Eval command, since default is symbolic SWARMSRCDIR
  swarm_dossrcdir=`eval cygpath -w $swarm_srcdir`
  DOSIFYEXPR='`cygpath -w $<`'
else
  top_dossrcdir=`cd $srcdir; pwd`
  swarm_dossrcdir=`cd $swarm_srcdir; pwd`
  DOSIFYEXPR='$<'
fi
AC_SUBST(DOSIFYEXPR)

AC_SUBST(top_dossrcdir)
AC_SUBST(swarm_dossrcdir)

AC_CHECK_FILE($catalog_file,,
[AC_MSG_ERROR(Failed to find XML catalog at $catalog_file)])
AC_SUBST(catalog_file)

AC_PATH_PROG(EMACS, emacs, missing)
AC_PATH_PROG(XSLTPROC, xsltproc, missing)
AC_PATH_PROG(PNGTOPNM, pngtopnm, missing)
AC_PATH_PROG(PNMTOPS, pnmtops, missing)
AC_PATH_PROG(PNMCROP, pnmcrop, missing)
AC_PATH_PROG(CJPEG, cjpeg, missing)

for i in EMACS XSLTPROC PNGTOPNM PNMTOPS PNMCROP CJPEG; do
if eval test '$ac_cv_path_'$i = missing ; then
AC_MSG_ERROR(Failed to find $i)
fi
done

AC_SUBST(XML_CATALOG_FILES)
md_PROG_GS

AC_OUTPUT([catalog.xml Makefile figs/Makefile refbook/Makefile installbook/Makefile overbook/Makefile set/Makefile refbook/activity/Makefile refbook/analysis/Makefile refbook/collections/Makefile refbook/defobj/Makefile refbook/gui/Makefile refbook/objectbase/Makefile refbook/random/Makefile refbook/random/extra/Makefile refbook/simtools/Makefile refbook/simtoolsgui/Makefile refbook/space/Makefile javadoc/Makefile javadoc/html/Makefile javadoc/latex/Makefile m4/Makefile tex/Makefile refbook/refbook.tex overbook/overbook.tex installbook/installbook.tex set/set.tex])
