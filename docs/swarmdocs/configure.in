AC_INIT(history.el)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(swarmdocs, 2003-09-08)

AM_MAINTAINER_MODE()

AC_PROG_LN_S()

if test "x$ACLOCAL" = xaclocal; then
  ACLOCAL="aclocal -I m4"
fi

AC_ARG_WITH(swarm,dnl
changequote(<,>)dnl
  --with-swarm=DIR        Swarm in DIR [\$SWARMSRCDIR],dnl
changequote([,])dnl
  swarm_srcdir=$withval,dnl
  swarm_srcdir='${SWARMSRCDIR}')

AC_ARG_WITH(catalog,dnl
  --with-catalog=FILE     Use catalog at FILE,dnl
  catalog_file=$withval,dnl
  catalog_file="")

AC_ARG_WITH(dtd-common,dnl
  --with-dtd-common=DIR   Common Declarations DTD in DIR,dnl
  commondtd_srcdir=$withval,dnl
  commondtd_srcdir="")

AC_ARG_WITH(dtd,dnl
changequote(<,>)dnl
  --with-dtd=DIR          DocBook DTD in DIR [${DOCBOOKDTDDIR:-/DocBookDTD}],dnl
changequote([,])dnl
  docbookdtd_srcdir=$withval,dnl
  docbookdtd_srcdir=${DOCBOOKDTDDIR:-/DocBookDTD})

AC_ARG_WITH(stylesheet,dnl
changequote(<,>)dnl
  --with-stylesheet=DIR   DocBook DSSSL in DIR [${DOCBOOKSTYLEDIR:-/DocBookStyleSheets}],dnl
changequote([,])dnl
  docbookstyle_srcdir=$withval,dnl
  docbookstyle_srcdir=${DOCBOOKSTYLEDIR:-/DocBookStyleSheets})

AC_ARG_WITH(gridturtle,dnl
changequote(<,>)dnl
  --with-gridturtle=DIR   GridTurtle in DIR [\$(swarm_srcdir)/../gridturtle],dnl
changequote([,])dnl
  gridturtle_srcdir=$withval,dnl
  gridturtle_srcdir=$swarm_srcdir/../gridturtle)

AC_ARG_WITH(javadoc,dnl
changequote(<,>)dnl
  --with-javadoc=DIR      Enable generation of Java documentation using JDK installed in DIR,dnl
changequote([,])dnl
  javadoc=$withval)
AC_MSG_CHECKING(do we build Java documentation?)

test -z "$javadoc" && javadoc=no
if test $javadoc = no; then
  AC_MSG_RESULT(no)
  JAVADOC=
  JAVADOCDIR=
else
  AC_MSG_RESULT(yes)
  AC_PATH_PROG(JAVADOC, javadoc, missing, $javadoc/bin)
  if test $JAVADOC != missing; then
    AC_MSG_RESULT(yes)
    JAVADOCDIR="javadoc"
  else
    AC_MSG_ERROR([Please specify a correct location for the JDK])
  fi
  AC_CHECK_PROG(LATEX, latex, latex, missing)
  if test $LATEX = missing; then
    AC_MSG_ERROR([Failed to find latex])
  fi
fi
AC_SUBST(JAVADOC)
AC_SUBST(JAVADOCDIR)

AM_CONFIG_HEADER(config.h)

AC_SUBST(swarm_srcdir)

dnl Change to the directory, since the text may be symbolic or a relative path
if test "$host_os" = cygwin; then
  top_dossrcdir=`cygpath -w $srcdir`
  dnl Eval command, since default is symbolic SWARMSRCDIR
  swarm_dossrcdir=`eval cygpath -w $swarm_srcdir`
  DOSIFYEXPR='`cygpath -w $<`'
else
  top_dossrcdir=$srcdir
  swarm_dossrcdir=$swarm_srcdir
  DOSIFYEXPR='$<'
fi
AC_SUBST(DOSIFYEXPR)

AC_SUBST(top_dossrcdir)
AC_SUBST(swarm_dossrcdir)

if test -z "$catalog_file" ; then
  test -n "$commondtd_srcdir" && catopts="-c $commondtd_srcdir/catalog"
  catopts="$catopts -c $docbookdtd_srcdir/docbook.cat -c $docbookstyle_srcdir/catalog"
  CATALOG_DTD_DELEGATE="DELEGATE \"-//OASIS//\" $docbookdtd_srcdir/docbook.cat"
else
  catopts="-c $catalog_file"
  CATALOG_DTD_DELEGATE="DELEGATE \"-//OASIS//\" $catalog_file"
fi
AC_SUBST(catopts)
AC_SUBST(catalog_file)
AC_SUBST(CATALOG_DTD_DELEGATE)

AC_SUBST(gridturtle_srcdir)

AC_PATH_PROG(EMACS, emacs, missing)
#AC_PATH_PROG(JADE, jade, missing)
AC_PATH_PROG(XSLTPROC, xsltproc, missing)
AC_PATH_PROG(SX, sx, missing)
AC_PATH_PROG(PNGTOPNM, pngtopnm, missing)
AC_PATH_PROG(PNMTOPS, pnmtops, missing)
AC_PATH_PROG(PNMCROP, pnmcrop, missing)
AC_PATH_PROG(CJPEG, cjpeg, missing)

dnl Don't use path here since TeX can't deal with Cygwin UNC paths.
AC_CHECK_PROG(JADETEX, jadetex, jadetex, missing)
AC_CHECK_PROG(DVIPS, dvips, dvips, missing)

for i in EMACS XSLTPROC PNGTOPNM PNMTOPS PNMCROP CJPEG; do
if eval test '$ac_cv_path_'$i = missing ; then
AC_MSG_ERROR(Failed to find $i)
fi
done

#for i in JADETEX DVIPS; do
#if eval test '$ac_cv_prog_'$i = missing ; then
#AC_MSG_ERROR(Failed to find $i)
#fi
#done

AC_SUBST(SGML_CATALOG_FILES)
md_PROG_GS

AC_OUTPUT([catalog.xml Makefile figs/Makefile refbook/Makefile installbook/Makefile overbook/Makefile set/Makefile refbook/activity/Makefile refbook/analysis/Makefile refbook/collections/Makefile refbook/defobj/Makefile refbook/gui/Makefile refbook/objectbase/Makefile refbook/random/Makefile refbook/random/extra/Makefile refbook/simtools/Makefile refbook/simtoolsgui/Makefile refbook/space/Makefile javadoc/Makefile javadoc/html/Makefile javadoc/latex/Makefile m4/Makefile tex/Makefile refbook/refbook.tex overbook/overbook.tex installbook/installbook.tex set/set.tex])
