AC_INIT(protocol.el)

AM_INIT_AUTOMAKE(swarmdocs, 1998-12-27)

AC_ARG_WITH(swarm,dnl
changequote(<,>)dnl
  --with-swarm=DIR        Swarm in DIR [\$SWARMSRCDIR],dnl
changequote([,])dnl
  swarm_srcdir=$withval,dnl
  swarm_srcdir='${SWARMSRCDIR}')

AC_ARG_WITH(dtd,dnl
changequote(<,>)dnl
  --with-dtd=DIR          DocBook DTD in DIR [${DOCBOOKDTDDIR:-/DocBookDTD}],dnl
changequote([,])dnl
  docbookdtd_srcdir=$withval,dnl
  docbookdtd_srcdir=${DOCBOOKDTDDIR:-/DocBookDTD})

AC_ARG_WITH(stylesheet,dnl
changequote(<,>)dnl
  --with-stylesheet=DIR   DocBook DSSSL in DIR [${DOCBOOKSTYLEDIR:-/DocBookStyleSheets}],dnl
changequote([,])dnl
  docbookstyle_srcdir=$withval,dnl
  docbookstyle_srcdir=${DOCBOOKSTYLEDIR:-/DocBookStyleSheets})

AC_ARG_WITH(gridturtle,dnl
changequote(<,>)dnl
  --with-gridturtle=DIR   GridTurtle in DIR [\$(swarm_srcdir)/../gridturtle],dnl
changequote([,])dnl
  gridturtle_srcdir=$withval,dnl
  gridturtle_srcdir=$swarm_srcdir/../gridturtle)

AM_CONFIG_HEADER(config.h)

AC_SUBST(swarm_srcdir)

dnl Change to the directory, since the text may be symbolic or a relative path
changequote(<,>)dnl
top_dossrcdir=`cd $srcdir; pwd | sed 's,//\([A-Za-z]\),\1:,'`
dnl Eval command, since default is symbolic SWARMSRCDIR
swarm_dossrcdir=`eval cd $swarm_srcdir ; pwd | sed 's,//\([A-Za-z]\),\1:,'`
changequote([,])dnl
AC_SUBST(top_dossrcdir)
AC_SUBST(swarm_dossrcdir)

AC_SUBST(docbookdtd_srcdir)
AC_SUBST(docbookstyle_srcdir)
AC_SUBST(gridturtle_srcdir)

AC_PATH_PROG(EMACS, emacs, missing)
AC_PATH_PROG(JADE, jade, missing)
AC_PATH_PROG(PNGTOPNM, pngtopnm, missing)
AC_PATH_PROG(PNMTOPS, pnmtops, missing)
AC_PATH_PROG(PNMCROP, pnmcrop, missing)
AC_PATH_PROG(CJPEG, cjpeg, missing)

dnl Don't use path here since TeX can't deal with Cygwin UNC paths.
AC_CHECK_PROG(JADETEX, jadetex, jadetex, missing)
AC_CHECK_PROG(DVIPS, dvips, dvips, missing)

for i in EMACS JADE PNGTOPNM PNMTOPS PNMCROP CJPEG; do
if eval test '$ac_cv_path_'$i = missing ; then
AC_MSG_ERROR(Failed to find $i)
fi
done

dnl TeX wants to look through path to find itself.
for i in JADETEX DVIPS; do
if eval test '$ac_cv_prog_'$i = missing ; then
AC_MSG_ERROR(Failed to find $i)
fi
done


AC_SUBST(SGML_CATALOG_FILES)
md_PROG_GS

AC_OUTPUT([catalog Makefile figs/Makefile refbook/Makefile installbook/Makefile overbook/Makefile set/Makefile refbook/activity/Makefile refbook/analysis/Makefile refbook/collections/Makefile refbook/defobj/Makefile refbook/gui/Makefile refbook/objectbase/Makefile refbook/random/Makefile refbook/simtools/Makefile refbook/simtoolsgui/Makefile refbook/space/Makefile refbook/tech/Makefile m4/Makefile tex/Makefile refbook/refbook.tex overbook/overbook.tex installbook/installbook.tex set/set.tex])

